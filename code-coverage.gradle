def publishedProjects = subprojects.findAll {
	(it.displayName.contains(":application:") && !it.displayName.contains(":application:periphery")) ||
			it.name == 'app-ui' ||
			it.name == 'integration-test"'
}

task jacocoMerge(type: JacocoMerge) {
	publishedProjects.each { subproject ->
		executionData fileTree(subproject.rootDir.absolutePath).include("**/build/jacoco/*.exec")
	}
}

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
	description = 'Generates an aggregate report from all subprojects'
	dependsOn publishedProjects.test, jacocoMerge

	additionalSourceDirs = files(publishedProjects.sourceSets.main.allSource.srcDirs)
	sourceDirectories = files(publishedProjects.sourceSets.main.allSource.srcDirs)
	classDirectories = files(publishedProjects.sourceSets.main.output)
	//executionData = files(publishedProjects.jacocoTestReport.executionData)
	executionData jacocoMerge.destinationFile

	reports {
		html.enabled = true // human readable
		xml.enabled = true // required by coveralls
	}

	afterEvaluate {
		classDirectories = files(classDirectories.files.collect {
			fileTree(dir: it,
					exclude: ['org/amhzing/clusterview/configuration/**',
							  'org/amhzing/clusterview/appui/configuration/**'])
		})
	}

	doFirst {
		executionData = files(executionData.findAll { it.exists() })
	}
}

coveralls {
	sourceDirs = publishedProjects.sourceSets.main.allSource.srcDirs.flatten()
	jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

tasks.coveralls {
	group = 'Coverage reports'
	description = 'Uploads the aggregated coverage report to Coveralls'

	dependsOn jacocoRootReport
	onlyIf { true }
}