buildscript {
	ext {
		NEXUS_REPO_URL = 'http://192.168.1.31:8082/nexus/content/repositories'

		springBootVersion = '1.5.1.RELEASE'
		propDepsVersion = '0.0.7'
		pitestVersion = '1.1.11'
		owaspDepCheckVersion = '1.4.5'
	}
	repositories {
		mavenCentral()

		maven {
			url 'https://plugins.gradle.org/m2/'
		}
		maven {
			url 'http://repo.spring.io/plugins-release'
		}
	}
	dependencies {
		classpath ("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath ("org.springframework.build.gradle:propdeps-plugin:${propDepsVersion}")
		classpath ("info.solidsoft.gradle.pitest:gradle-pitest-plugin:${pitestVersion}")
		classpath ("org.owasp:dependency-check-gradle:${owaspDepCheckVersion}")
	}
}

plugins {
	// Build scan should be first
	id 'com.gradle.build-scan' version '1.4'

	id 'java'
	id 'maven'
	id 'jacoco'
	id 'idea'
	id 'jdepend'
	id 'findbugs'
	id 'pmd'

	id "io.spring.dependency-management" version "1.0.0.RELEASE"
	id 'net.saliman.cobertura' version '2.4.0'
	id 'com.github.kt3k.coveralls' version '2.7.1'
	id 'net.researchgate.release' version '2.4.0'
	id 'com.gorylenko.gradle-git-properties' version '1.4.17'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'info.solidsoft.pitest'
apply plugin: 'org.owasp.dependencycheck'

configure(allprojects) {
	apply plugin: 'propdeps'
	apply plugin: 'propdeps-maven'
	apply plugin: 'propdeps-idea'
	apply plugin: 'propdeps-eclipse'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

subprojects {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'java'

	dependencyManagement {
		imports {
			mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
		}
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.14'
}

buildScan {
	licenseAgreementUrl = 'https://gradle.com/terms-of-service'
	licenseAgree = 'yes'
}

// Dependencies
apply from: 'gradle-segment/dependencies.gradle'

// Test
apply from: 'gradle-segment/test.gradle'

// Code analysis
apply from: 'gradle-segment/code-analysis.gradle'

// Publish to Nexus
apply from: 'gradle-segment/publish.gradle'


// Git properties that display in the actuator /info endpoint
gitProperties {
	dateFormat = "yyyy-MM-dd'T'HH:mm:ss.SSSZ z"
	dateFormatTimeZone = "UTC"
}

springBoot {
	excludeDevtools = true

	buildInfo {
		additionalProperties = [
				'description': "${description}"
		]
	}
}

// Make the System.properties available in the bootRun task.
bootRun {
	systemProperties = System.properties
}
